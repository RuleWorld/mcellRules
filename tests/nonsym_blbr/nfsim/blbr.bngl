# tlbr.bngl
# Trivalent ligand - bivalent receptor model [Peaking Params]
#
# Theoretical model was published and analyzed in:
#   B Goldstein and AS Perelson.
#   "Equilibrium theory for the clustering of bivalent cell surface receptors by trivalent ligands".
#   Biophysical Journal 45, 1109–1123 (1984).
#
# Rule-based adaptation appeard as part of:
#   MW Sneddon, JR Faeder, and T Emonet.
#   "Efficient modeling, simulation and coarse-graining of biological complexity with NFsim".
#   Nature Methods 8, 177–183 (2011).
setOption("SpeciesLabel","HNauty")
begin model
begin parameters
    # constants
    NA  6.0221e23             # Avogadro's number
    # experimental conditions
    cell_density  1e9         # cells per liter
    Rec_cell      3e5         # receptors per cell
    Lig_conc      8.3e-9      # initial ligand concentration, M
    # simulation scale
    f    0.001                # fraction of simulated cell     
    Vol  1e-15*1055.1    #(1/cell_density)*f   # reactor volume
  	# initial species count
	Lig_tot  Lig_conc*NA*Vol
	Rec_tot  Rec_cell*f
    # equillibrium constants
    K1       0.1084e9         # free binding equillibrium constant, /M
    K2       33.72e9          # crosslinking equillibrium constant, /M
    # kinetic parameters
    koff     0.01             # unbinding rate, /s
    kp1      K1*koff          # Free binding rate constant, /M/s
	kp2      K2*koff          # Crosslinking rate constant, /M/s
end parameters
begin molecule types
    L(r2,r1)
    R(l2,l1)
end molecule types
begin seed species
     L(r1,r2)  5973
     R(l1,l2)  300
end seed species
begin observables
    Species Clusters L(r1!+,r2!+)
    Molecules LRmotif R(l2!0).L(r1!0), R(l1!0).L(r1!0), R(l2!0).L(r2!0), R(l1!0).L(r2!0)
    Molecules Lfreesite L(r1), L(r2)
    Molecules Rfreesite R(l1), R(l2)
    Species Lmonomer L(r1,r2)
    Species Rmonomer R(l1,l2)
    Molecules Ltot L
    Molecules Rtot R
end observables
begin reaction rules
    R(l1) + L(r1,r2) -> L(r1!1,r2).R(l1!1) kp1/NA/Vol
    R(l1) + L(r1,r2) -> L(r1,r2!1).R(l1!1) kp1/NA/Vol
    R(l2) + L(r1,r2) -> L(r1!1,r2).R(l2!1) kp1/NA/Vol
    R(l2) + L(r1,r2) -> L(r1,r2!1).R(l2!1) kp1/NA/Vol
    R(l1) + L(r1,r2!+) -> R(l1!1).L(r1!1,r2!+) kp2/NA/Vol
    R(l1) + L(r1!+,r2) -> R(l1!1).L(r1!+,r2!1) kp2/NA/Vol
    R(l2) + L(r1,r2!+) -> R(l2!1).L(r1!1,r2!+) kp2/NA/Vol
    R(l2) + L(r1!+,r2) -> R(l2!1).L(r1!+,r2!1) kp2/NA/Vol
end reaction rules
end model
    
simulate({method=>"nf",t_start=>0,t_end=>40,n_steps=>100,verbose=>1})

