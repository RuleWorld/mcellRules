ITERATIONS = 100000
TIME_STEP = 5e-06
VACANCY_SEARCH_DISTANCE = 100
 
INCLUDE_FILE = "icogeometry.mdl"
 
DEFINE_SURFACE_CLASSES
{
   reflect {
    REFLECTIVE = ALL_MOLECULES
   }
}
 
MODIFY_SURFACE_REGIONS
{

   CP[ALL]
   {
      SURFACE_CLASS = reflect
   }
} 

 
/* Model Parameters */
   Nav = 6.022e8               /* Avogadro number based on a volume size of 1 cubic um */
   rxn_layer_t = 0.01
   vol_wall = 56.5695045056029  /*Surface area*/
   vol_EC = 39
   vol_PM = 4.836624601  /*Surface area*/
   vol_CP = 1
   NaV = 602200000
   Lig_tot = 250
   Rec_tot = 500
   kp1 = 100
   km1 = 0.01
   kp2 = 100
   km2 = 0.01
 
/* Diffusion bloc */
   T = 298.15      /* Temperature, K */
   h = rxn_layer_t      /* Thickness of 2D compartment, um */
   Rs = 0.002564      /* Radius of a (spherical) molecule in 3D compartment, um */
   Rc = 0.0015      /* Radius of a (cylindrical) molecule in 2D compartment, um */
   gamma = 0.5722      /* Euler's constant */
   KB = 1.3806488e-19     /* Boltzmann constant, cm^2.kg/K.s^2 */
   mu_wall = 1e-9      /* Viscosity in compartment wall, kg/um.s */
   mu_EC = 1e-9      /* Viscosity in compartment EC, kg/um.s */
   mu_PM = 1e-9      /* Viscosity in compartment PM, kg/um.s */
   mu_CP = 1e-9      /* Viscosity in compartment CP, kg/um.s */
 



#DEFINE_MOLECULES
{
  Lig(l,l, p~0~1)
  {
      DIFFUSION_CONSTANT_2D = KB*T*LOG((mu_PM*h/(Rc*(mu_EC+mu_CP)/2))-gamma)/(4*PI*mu_PM*h)
 
  }
  Rec(a){
      DIFFUSION_CONSTANT_2D = KB*T*LOG((mu_PM*h/(Rc*(mu_EC+mu_CP)/2))-gamma)/(4*PI*mu_PM*h)
      
  }
}


 
 
#DEFINE_REACTIONS
{
    /* Ligand-receptor binding      */
   Rec(a) + Lig(l,l) <-> Rec(a!1).Lig(l!1,l)  [kp1, km1]
    /* Receptor-aggregation */
   Rec(a) + Lig(l,l!+) <-> Rec(a!2).Lig(l!2,l!+)  [kp2, km2]

   Lig(l!+,l!+,p~0) -> Lig(l!+,l!+,p~1)  [0.1]
   Lig(p~1) -> Lig(p~0)  [0.05]
}
 
 
#INSTANTIATE Scene OBJECT
{
  EC OBJECT EC {}
  CP OBJECT CP {
    PARENT = EC
    MEMBRANE = PM OBJECT CP[ALL]
  }

   ligand_rel RELEASE_SITE
   {
    SHAPE = Scene.CP[ALL]
    MOLECULE = Lig(l,l,p~0)@PM
    NUMBER_TO_RELEASE = Lig_tot
    RELEASE_PROBABILITY = 1
   }
      receptor_rel RELEASE_SITE
   {
    SHAPE = Scene.CP[ALL]
    MOLECULE = Rec(a)@PM 
    NUMBER_TO_RELEASE = Lig_tot
    RELEASE_PROBABILITY = 1

   }
   /*complex_rel RELEASE_SITE
   {
    SHAPE = Scene.CP[ALL]
    MOLECULE = Lig(l!1,l).Rec(a!1)@PM 
    NUMBER_TO_RELEASE = Rec_tot
    RELEASE_PROBABILITY = 1

   }*/
}
 

 
/* Observables bloc */
#REACTION_DATA_OUTPUT
{
   STEP = 1e-6
 
   /*RecFree*/
   { COUNT[Rec(a)@PM, WORLD] }=> "./react_data/RecFree.dat"

   /*Ligfree*/
   { COUNT[Lig(l,l)@PM, WORLD] }=> "./react_data/LigFree.dat"
 
   /*RecBound*/
   { COUNT[Lig(l!+,l)@EC, WORLD] + COUNT[@PM::Lig(l!+,l!+)@EC,WORLD] }=> "./react_data/RecBound.dat"
 
   /*Dimers*/
   { COUNT[Lig(l!+,l!+)@EC,WORLD] }=> "./react_data/Dimers.dat"
 
}


VIZ_OUTPUT {
   MODE = CELLBLENDER
   FILENAME = "./viz_data/rec_dim"
   MOLECULES
   {
      NAME_LIST {ALL_MOLECULES}
      ITERATION_NUMBERS {ALL_DATA @ [1, 100, [200 TO 100000 STEP 100]]
      }
   }
}